buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "0.1.0"
group= "twistedgate.immersiveposts"
archivesBaseName = "ImmersivePosts"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "1.12.2-14.23.5.2768"
	runDir = "run"
	mappings = "stable_39"
}

repositories {
	maven {
		url = "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name = "CurseForge"
		url = "https://minecraft.curseforge.com/api/maven/"
	}
}

dependencies {
	deobfCompile "immersive-engineering:ImmersiveEngineering:0.12:88"
}
	
task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty('keyStore')
	}
	
	if (project.hasProperty('keyStore')) {
		alias = project.alias
		storePass = project.storePass
		keyPass = project.storePass
		keyStore = project.keyStore
		
		inputFile = jar.archivePath
		outputFile = jar.archivePath
	}
}

build.dependsOn signJar

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version
	
	from(sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		
		expand "version": project.version, "mcversion": project.minecraft.version
	}
	
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}
