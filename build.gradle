//import net.minecraftforge.gradle.common.tasks.SignJar

plugins {
	id "java-library"
	id "maven-publish"
	id "idea"
	id "eclipse"
	id "net.neoforged.moddev" version "$version_mdg"
}


java.toolchain.languageVersion = JavaLanguageVersion.of(21)

//----------------------------------------------------------------------------------------------------------------------

version = mod_version
group = mod_group
base.archivesName = "ImmersivePosts-"+version_minecraft

sourceSets {
	main {
		resources.srcDirs = ["$rootDir/src/main/resources", "$rootDir/src/generated/resources"]
	}
	datagen {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
		compileClasspath += main.output
		runtimeClasspath += main.output
	}
}

neoForge {
	version = version_neo
	
	// accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')
	
	runs {
		configureEach {
			systemProperty "forge.logging.markers", "REGISTRIES"

			logLevel = org.slf4j.event.Level.DEBUG
		}
		
		client {
			client()
		}
		
		server {
			server()
			programArgument "--nogui"
		}
		
		data {
			data()
			sourceSet = sourceSets.datagen
			programArguments = [
					"--mod", "${mod_id}",
					"--all",
					"--output", file("src/generated/resources/").toString(),
					"--validate",
					"--existing", sourceSets.main.resources.srcDirs[0].toString(),
					"--existing-mod", "immersiveengineering"
			]
		}
	}
	
	mods {
		"${mod_id}" {
			sourceSet sourceSets.main
			sourceSet sourceSets.datagen
		}
	}
}

configurations {
	runtimeClasspath.extendsFrom localRuntime
}

repositories {
	maven {	name = "BlameJared"; url = "https://maven.blamejared.com/" } // IE files
}

dependencies {
	implementation "blusunrize.immersiveengineering:ImmersiveEngineering:${version_ie}"
	datagenImplementation "blusunrize.immersiveengineering:ImmersiveEngineering:${version_ie}:datagen"
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8" // Use the UTF-8 charset for Java compilation
}

jar {
	manifest {
		attributes([
			"Specification-Title": "immersiveposts",
			"Specification-Vendor": "TwistedGate",
			"Specification-Version": "1",
			"Implementation-Title": project.name,
			"Implementation-Version": project.version,
			"Implementation-Vendor" :"TwistedGate",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.FAIL
	exclude ".cache"
}
var modMeta = tasks.register("generateModMetadata", ProcessResources) {
	var version_replaces = [
			'ipo_version'	: mod_version,
			'mc_version'	: version_minecraft,
			'neo_version'	: version_neo,
			'ie_version'	: version_ie,
	]

	inputs.properties version_replaces
	expand version_replaces
	from "src/main/templates"
	into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask modMeta

/*
signJar {
	onlyIf { project.hasProperty('keyStore') }
	if (project.hasProperty('keyStore')) {
		alias: alias
		storePass: storePass
		keyPass: storePass
		keyStore: keyStore
	}
}

afterEvaluate {
    signJar.dependsOn reobfJar
}
*/
